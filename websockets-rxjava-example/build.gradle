apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        applicationId "com.appunite.socket"
    }

    flavorDimensions "config"
    productFlavors {
        rxjava {
            dimension "config"
        }

        rxjava2 {
            dimension "config"
            applicationIdSuffix ".rxjava2"
            versionNameSuffix "-rxjava2"
        }
    }
    buildTypes {
        release {
            proguardFiles = [getDefaultProguardFile('proguard-android.txt'), 'src/main/proguard-project.txt']
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        textOutput "stdout"
        textReport true
    }

    compileOptions {
        sourceCompatibility rootProject.javaSourceCompatibility
        targetCompatibility rootProject.javaTargetCompatibility
    }
}

dependencies {
    rxjavaImplementation project(":websockets-rxjava"),
        rootProject.libraries.rxandroid,
        rootProject.libraries.rxbinding
    rxjava2Implementation project(":websockets-rxjava2"),
        rootProject.libraries.rxandroid2,
        rootProject.libraries.rxbinding2
    implementation rootProject.libraries.support_v4,
        rootProject.libraries.recyclerview_v7,
        rootProject.libraries.gson,
        rootProject.libraries.guava
    //compile "com.appunite:websockets:1.0"

    testImplementation rootProject.basic_test_dependencies
    testImplementation rootProject.libraries.guava
}
